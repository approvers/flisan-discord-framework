buildscript {
    // プロジェクトの情報を設定する
    def versionId = [0, 1, 0]
    ext {
        pj = [
            versions     : [
                name: "${versionId[0]}.${versionId[1]}.${versionId[2]}",
                code: versionId[0] * 10000 + versionId[1] * 100 + versionId[2]
            ],
            groupId      : "dev.approvers",
            artifactId   : "jubilant",
            siteUrl      : "https://github.com/approvers/jubilant",
            githubUrl    : "https://github.com/approvers/jubilant",
            scmConnection: "scm:git:https://github.com/approvers/jubilant"
        ]
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group "dev.approvers"
version "0.1.0"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.5"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "net.dv8tion:JDA:4.1.1_149"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        bintray(MavenPublication) {
            groupId = pj.groupId
            artifactId = pj.artifactId
            version = pj.versions.name
            from components.java
            pom.withXml {
                def licenses = node.appendNode("licenses")
                appendLicense(licenses, "The MIT License", "https://opensource.org/licenses/MIT", "repo")
                appendScm(node, pj.scmConnection, pj.githubUrl)
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintray_user") ? bintray_user : ""
    key = project.hasProperty("bintray_key") ? bintray_key : ""
    publications = ["bintray"]

    pkg {
        repo = "Jubilant"
        userOrg = "approvers"
        name = pj.groupId + "." + pj.artifactId
        licenses = ["MIT"]
        websiteUrl = pj.siteUrl
        vcsUrl = pj.githubUrl + ".git"
        issueTrackerUrl = pj.githubUrl + "/issues"
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

static def appendLicense(parentNode, name, url, distribution) {
    def node = parentNode.appendNode("license")
    node.appendNode("name", name)
    node.appendNode("url", url)
    node.appendNode("distribution", distribution)
}

static def appendScm(parentNode, connection, url) {
    def node = parentNode.appendNode("scm")
    node.appendNode("connection", connection)
    node.appendNode("url", url)
}
